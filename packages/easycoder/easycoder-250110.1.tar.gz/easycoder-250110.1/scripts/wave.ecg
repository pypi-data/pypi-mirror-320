!   Mexican Wave

    script MexicanWave

    rectangle Bar
    variable Spec
    variable Item
    variable Angle
    variable Height
    variable Mid
    variable Range
    variable Step
    variable H
    variable L
    variable N

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Create the window (starts the graphics)

    create window title `Mexican Wave` at 300 300 size 640 480 fill color 255 255 200

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Define a set of 10 bars

    put `{
        `   "#": [
        `       "Bar0",
        `       "Bar1",
        `       "Bar2",
        `       "Bar3",
        `       "Bar4",
        `       "Bar5",
        `       "Bar6",
        `       "Bar7",
        `       "Bar8",
        `       "Bar9"
        `   ],
        `
        `` into Spec

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Create the specifications for each of the 10 barsm then render the spec

    put 0 into N
    while N is less than 10
    begin
        put `
        `   "Bar<N>": {
        `      "type": "rectangle",
        `      "left": "<L>w",
        `      "bottom": 0,
        `      "width": "8w",
        `      "height": 0,
        `      "fill": "blue",
        `      "id": "bar<N>"
        `   }` into Item

        multiply N by 10 giving L
        add 1 to L
        replace `<L>` with L in Item
        replace `<N>` with N in Item
        put Spec cat Item into Spec
        increment N
        if N is less than 10 put Spec cat `,` cat newline into Spec
    end
    put Spec cat newline cat `}` cat newline into Spec

    render spec Spec

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Attach a script variable to each of the 10 bars

    divide the window height by 2 giving Mid
    multiply Mid by 9 giving Range
    divide Range by 10

    set the elements of Bar to 10
    set the elements of Angle to 10
    put 0 into N
    while N is less than 10
    begin
        index Bar to N
        attach Bar to `bar` cat N
        index Angle to N
        multiply N by 36 giving Angle
        increment N
    end

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! This section animates the bars growing up fro the bottom of the screen

    wait 1
    put 0 into N
    while N is less than 10
    begin
        index Bar to N
        index Angle to N
        put sin Angle radius Range into Height
        negate Height
        add Mid to Height
        divide Height by 10 giving Step
        put 0 into H
        while H is less than Height
        begin
            set attribute `height` of Bar to H
            add Step to H
            wait 1 milli
        end
        set attribute `height` of Bar to Height
        increment N
    end
    on click Bar print `Click ` cat the index of Bar
    end

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! This section runs the main animation
    wait 1
    while true
    begin
        put 0 into N
        while N is less than 10
        begin
            index Bar to N
            index Angle to N
            put sin Angle radius Range into Height
            negate Height
            add Mid to Height
            set attribute `height` of Bar to Height
            if Angle is 0 put 360 into Angle
            decrement Angle
            increment N
        end
        wait 1 milli
    end
